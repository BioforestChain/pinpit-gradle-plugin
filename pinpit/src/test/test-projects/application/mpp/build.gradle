plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.multiplatform"
    id "org.jetbrains.compose"
    id "de.mobanisto.pinpit"
}

repositories {
    jetbrainsCompose()
    google()
}

kotlin {
    // empty stub (no actual android app) to detect configuration conflicts
    // like https://github.com/JetBrains/compose-jb/issues/2345
    android()

    jvm("desktop")
    sourceSets {
        desktopMain {
            dependsOn(commonMain)

            dependencies {
                implementation(compose.desktop.currentOs)
            }
        }
    }
}

android {
    compileSdk = 31

    defaultConfig {
        minSdk = 21
        targetSdk = 31
    }
}

ext.versionCode = "1.0.0"

pinpit.desktop {
    application {
        mainClass = "MainKt"
        nativeDistributions {
            jvmVendor = "PINPIT_JVM_VENDOR"
            jvmVersion = "PINPIT_JVM_VERSION"

            packageVersion = versionCode
            packageName = "TestPackage"
            description = "Test description"
            copyright = "Test Copyright Holder"
            vendor = "Test Vendor"

            linux {
                shortcut = true
                packageName = "test-package"
                debMaintainer = "example@example.com"
                menuGroup = "menu-group"
                debPackageVersion = versionCode
                appCategory = "utils"
                menuGroup = "System;Utility;"
                deb("UbuntuFocalX64") {
                    qualifier = "ubuntu-20.04"
                    arch = "x64"
                    depends("libc6", "libexpat1", "libgcc-s1", "libpcre3", "libuuid1", "xdg-utils", "zlib1g", "libnotify4")
                }
                deb("UbuntuBionicX64") {
                    qualifier = "ubuntu-18.04"
                    arch = "x64"
                    depends("libasound2", "libc6", "libexpat1", "libfontconfig1", "libfreetype6", "libgcc1",
                            "libglib2.0-0", "libgraphite2-3", "libharfbuzz0b", "libjpeg-turbo8", "liblcms2-2",
                            "libpcre3", "libpng16-16", "libstdc++6", "xdg-utils", "zlib1g", "libnotify4")
                }
                deb("DebianBullseyeX64") {
                    qualifier = "debian-bullseye"
                    arch = "x64"
                    depends("libasound2", "libbrotli1", "libc6", "libexpat1", "libfontconfig1", "libfreetype6",
                            "libgcc-s1", "libglib2.0-0", "libgraphite2-3", "libharfbuzz0b", "libjpeg62-turbo",
                            "liblcms2-2", "libpcre3", "libpng16-16", "libstdc++6", "libuuid1", "xdg-utils", "zlib1g",
                            "libnotify4")
                }
            }
            windows {
                console = true
                dirChooser = true
                perUserInstall = true
                shortcut = true
                upgradeUuid = "2d6ff464-75be-40ad-a256-56420b9cc374"
                packageVersion = versionCode
                iconFile.set(project.file("src/desktopMain/packaging/windows/hello.ico"))
                msi() {
                    arch = "x64"
                    bitmapBanner.set(project.file("src/desktopMain/packaging/windows/banner.bmp"))
                    bitmapDialog.set(project.file("src/desktopMain/packaging/windows/dialog.bmp"))
                }
            }
        }
    }
}
