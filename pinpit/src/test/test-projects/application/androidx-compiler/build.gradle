import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id "org.jetbrains.kotlin.jvm"
    id "org.jetbrains.compose"
    id "de.mobanisto.pinpit"
}

repositories {
    google()
    jetbrainsCompose()
}

def attributeUsage = Attribute.of("org.gradle.usage", String)

configurations {
    currentOs {
        extendsFrom implementation
        attributes { attribute(attributeUsage, "java-runtime") }
    }
    windowsX64 {
        extendsFrom implementation
        attributes { attribute(attributeUsage, "java-runtime") }
    }
    linuxX64 {
        extendsFrom implementation
        attributes { attribute(attributeUsage, "java-runtime") }
    }
}

sourceSets {
    main {
        java {
            compileClasspath = configurations.currentOs
            runtimeClasspath = configurations.currentOs
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    currentOs compose.desktop.currentOs
    windowsX64 compose.desktop.windows_x64
    linuxX64 compose.desktop.linux_x64
}

compose {
    kotlinCompilerPlugin.set("COMPOSE_COMPILER_ARTIFACT_PLACEHOLDER")
}

ext.versionCode = "1.0.0"

pinpit.desktop {
    application {
        mainClass = "Main"
        def projectPath = project.projectDir.absolutePath
        if (DefaultNativePlatform.currentOperatingSystem.isWindows()) {
            projectPath = projectPath.replace("\\", "\\\\")
        }
        args(projectPath)
        nativeDistributions {
            jvmVendor = "PINPIT_JVM_VENDOR"
            jvmVersion = "PINPIT_JVM_VERSION"

            packageVersion = versionCode
            packageName = "TestPackage"
            description = "Test description"
            copyright = "Test Copyright Holder"
            vendor = "Test Vendor"

            linux {
                shortcut = true
                packageName = "test-package"
                debMaintainer = "example@example.com"
                menuGroup = "menu-group"
                debPackageVersion = versionCode
                appCategory = "utils"
                menuGroup = "System;Utility;"
                debPreInst.set(project.file("src/main/packaging/deb/preinst"))
                debPostInst.set(project.file("src/main/packaging/deb/postinst"))
                debPreRm.set(project.file("src/main/packaging/deb/prerm"))
                debPostRm.set(project.file("src/main/packaging/deb/postrm"))
                debCopyright.set(project.file("src/main/packaging/deb/copyright"))
                debLauncher.set(project.file("src/main/packaging/deb/launcher.desktop"))
                deb("UbuntuFocalX64") {
                    qualifier = "ubuntu-20.04"
                    arch = "x64"
                    depends("libc6", "libexpat1", "libgcc-s1", "libpcre3", "libuuid1", "xdg-utils", "zlib1g", "libnotify4")
                }
            }
            windows {
                console = true
                dirChooser = true
                perUserInstall = true
                shortcut = true
                menu = true
                menuGroup = "compose"
                upgradeUuid = "2d6ff464-75be-40ad-a256-56420b9cc374"
                packageVersion = versionCode
                msi() {
                    arch = "x64"
                }
            }
            macOS {
                dockName = "CustomDockName"
            }
        }
    }
}
